#
# Sample sofa model that exercises all the supported elements.
#

import "simple_all.sofa"
import style "default.css"

# Comes in two style. Just a list (profile will be basic) or under different profiles.
stereotype Regulatory: [GDPR, SOX]
stereotype Security: [PIDRelevant]

actor Client
actor RelationshipManager

primitives: [String, Boolean]

class Person:
    stereotypes: [Regulatory.GDPR, Security.PIDRelevant]
    documentation: |
        Represents a person, who could be either a customer or a relationship manager
    attributes:
        name: 
            cardinality: 1
            type: String
            visibility: public
    operations:
        fullName:
            visibility: public
            parameters:
                firstName:
                    type: String
                lastName:
                    type: String
                fullName:
                    type: String
                    direction: return
        address:
            parameters: [street, city, country]

interface Product:
    attributes:
        name: 
            cardinality: 1
            type: String
        is_active:
            cardinality: 0..1
            type: Boolean

class Account
# class Product :: TODO: Need to handle duplicate names with qname approach

relation Account ..|> Product

class CurrentAccount
class SavingsAccount

relation CurrentAccount --|> Account
relation SavingsAccount --|> Account

relation Person <>-> Product:
    source:
        cardinality: 0..1
    target:
        cardinality: 1..*

