
%declare _INDENT _DEDENT
%import common (CNAME, WS_INLINE, WS)
%ignore WS_INLINE

?start: sofa
sofa: (imports | actors | components | classes | interfaces | relations | stereotypes | diagrams)+

# Terminals -----

COMMENT     : /#.*/ _NL*
_COLON      : ":"
_NL         : /((\r+|\n+|(\r\n)+)(\t|\s)*)/
_PIPE       : "|"
_COMMA      : ","
_LBRA       : "["
_RBRA       : "]"
_QUOTE      : "\""
_AT         : "@"

STRING          : /(?!\[).+/
SIMPLE_STRING   : ("a".."z"|"A".."Z"|"0".."9"|" "|"_")+
INTEGER         : ("0".."9")+
DECIMAL         : INTEGER? "." INTEGER
NUMBER          : INTEGER | DECIMAL
QUOTED_STRING   : _QUOTE STRING _QUOTE

%ignore COMMENT

# Struct Rules ------

name: CNAME | QUOTED_STRING
scalar: STRING

_string_newline: STRING _NL?
multiline_scalar: _string_newline+

_vector_item: (SIMPLE_STRING | QUOTED_STRING) (_COMMA|_NL)?
vector_items: _vector_item+
vector: _LBRA vector_items _RBRA

multiline_vector_body: _NL _INDENT ("-" (map | name) _NL?)+ _DEDENT

key_value: name _COLON scalar _NL?
vector_value: name _COLON vector _NL?
multiline_value: name _COLON _PIPE _NL _INDENT multiline_scalar _DEDENT
multiline_vector_value: name _COLON multiline_vector_body

map: name _COLON _NL properties
properties: _INDENT (vector_value | multiline_vector_value | key_value | map | multiline_value)+ _DEDENT

# Element Rules ----

element_body: _COLON _NL properties

# ---
actor: "actor" name element_body? _NL*
actors: actor+

# ---
association: "->"
bidirectional: "<->"
inheritance: "-|>"
information_flow: ".>"

port: _AT (CNAME|NUMBER)
relation_name: name port?

relation_type: (association | bidirectional | inheritance | information_flow)

relation: "relation" relation_name relation_type relation_name element_body? _NL*
relations: relation+

# ---

stereotype: "stereotypes" _COLON vector _NL*
stereotypes: stereotype+

# ---

diagram: "diagrams" _COLON (vector | multiline_vector_body) _NL*
diagrams: diagram+

# ---

style: "style"
import: "import" style? name _NL*
imports: import+

# --- Type defs ---

param_list: "(" vector_items* ")"
typedef_body: name param_list? element_body? _NL*

# ---
component: "component" typedef_body
components: component+

# ---

class: "class" typedef_body
classes: class+

# ---

interface: "interface" typedef_body
interfaces: interface+


